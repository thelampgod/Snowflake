plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
}

ext {
    buildVersion = '1.0-SNAPSHOT'

    sqliteVersion = '3.44.1.0'
    commonsVersion = "3.12.0"
}

group "com.github.thelampgod"
version "$buildVersion"

tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
}

repositories {
    mavenCentral()
}

configurations {
    jarLibs
    implementation.extendsFrom jarLibs
}

dependencies {
    jarLibs "org.apache.commons:commons-lang3:$commonsVersion"
    jarLibs 'com.google.guava:guava:33.0.0-jre'

    jarLibs "com.google.code.gson:gson:2.9.1"

    //database
    jarLibs "org.xerial:sqlite-jdbc:$sqliteVersion"
    jarLibs "org.apache.commons:commons-dbcp2:2.9.0"

    jarLibs(group: 'net.minecrell', name: 'terminalconsoleappender', version: '1.2.0')
}

def targetJavaVersion = 17
tasks.withType(JavaCompile).configureEach {
    // ensure that the encoding is set to UTF-8, no matter what the system default is
    // this fixes some edge cases with special characters not displaying correctly
    // see http://yodaconditions.net/blog/fix-for-java-file-encoding-problems-with-gradle.html
    // If Javadoc is generated, this must be specified in that task too.
    it.options.encoding = "UTF-8"
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        it.options.release = targetJavaVersion
    }
}

java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
    archivesBaseName = project.name
}

jar {
    dependsOn shadowJar

    from("LICENSE") {
        rename { "${it}_${project.name}" }
    }

    manifest {
        attributes "Implementation-Title": "thelampgod",
                "Implementation-Version": "$buildVersion",
                "Main-Class": "com.github.thelampgod.snowflake.Main"
    }
}

shadowJar {
    configurations = [project.configurations.jarLibs]
    archiveClassifier.set('shadow')
}